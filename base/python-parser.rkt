#lang racket

(require racket/match
	 "python-lexer.rkt"
	 "python-grammar.rkt")

(provide parse-python)

#| 

The AST expected by get-structured-python is a symbol -> string | number | AST dictionary as produced by Python's AST library. The main non-trivial aspect of it is the 'ctx attribute of each expression node, which is (probably) determined by the statement and expression productions.

The AST generated by ragg is syntax-object, which is nested just like the grammar productions, e.g. really deeply.

Example:
(syntax->datum (parse '(NAME NEWLINE)))
'(file_input
  (stmt
   (simple_stmt
    (small_stmt
     (expr_stmt
      (testlist (test (or_test (and_test (not_test (comparison (expr (xor_expr (and_expr (shift_expr (arith_expr (term (factor (power (atom NAME)))))))))))))))))
    NEWLINE)))

|#

(define (parse-python port)
  (py-ragg->python-ast (syntax->datum (parse (get-python-lexer port)))))

(define ast hasheq)

(define (py-ragg->python-ast py-ragg)
  (match py-ragg
    [(list 'file_input stmts ...)
     (ast 'nodetype "Module"
	   'body (map py-ragg->python-ast stmts))]
    
    [(list 'stmt stmt) (py-ragg->python-ast stmt)]
    
    [(list 'simple_stmt stmt NEWLINE) (py-ragg->python-ast stmt)] ; Todo: simple-stmt-multi.py
    
    [(list 'small_stmt stmt) (py-ragg->python-ast stmt)]
    
    ; TODO: Allow only assignments to those allowed by http://docs.python.org/3.2/reference/simple_stmts.html#assignment-statements
    ; TODO: assign-chain.py
    ; TODO: augassign.py
    ; TODO: augassign-yield.py
    [(list 'expr_stmt testlist "=" val)
     (ast 'nodetype "Assign"
	   'targets (list (py-ragg-expr->python-ast testlist "Store"))
	   'value (py-ragg-expr->python-ast val "Load"))]

    [(list 'expr_stmt val)
     (ast 'nodetype "Expr"
	  'value (py-ragg-expr->python-ast val "Load"))]
    
    [_ 
     (display "=== Unhandled grammar ===\n")
     (pretty-write py-ragg)
     (error (string-append "Unhandled grammar"))]))

; Destructure ragg python expressions to python-ast with ctx values
(define (py-ragg-expr->python-ast py-ragg expr-ctx)
  (match py-ragg

    #| Expression fallthroughs... |#
    [(list (or 'testlist 'test 'or_test 'and_test 'not_test 'comparison 'expr 'xor_expr 'and_expr 'shift_expr 'arith_expr 'term 'factor 'power) expr)
     (py-ragg-expr->python-ast expr expr-ctx)]

    

    ; Note expr-ctx (this may be wrong)
    [(list 'atom (cons 'name name))
     (ast 'nodetype "Name"
	   'id name
	   'ctx (ast 'nodetype expr-ctx))]

    [(list 'atom (cons type val))
     (if (equal? expr-ctx "Store")
	 (error "Cannot store to a literal")
	 (cond [(member type '(integer float imaginary)) 
		(ast 'nodetype "Num"
		      'n val)]
	       [else (error "Literal not handled yet")]))]

    [_ 
     (display "=== Unhandled expression ===\n")
     (pretty-write py-ragg)
     (error (string-append "Unhandled expression"))]))
